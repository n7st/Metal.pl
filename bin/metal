#!/usr/bin/env perl

use strict;
use warnings;

use FindBin;
use Getopt::Long;
use Pod::Usage;

use lib "$FindBin::RealBin/../lib";

use Metal;

################################################################################

my %options;

Getopt::Long::GetOptions(
    "run|r"    => \$options{run_bot},
    "mode|m=s" => \$options{mode},

    # Options for a new Handler
    "type|t=s" => \$options{type},
    "name|n=s" => \$options{name},
);

if ($options{mode} && $options{mode} eq 'run') {
    $options{run_bot} = 1;
}

$options{mode} ||= '';

################################################################################

sub main {
    my $metal = Metal->new();

    if ($options{run_bot}) {
        $metal->run();
    } elsif ($options{mode} eq 'test') {
        # TODO check dzil exists?
        system("dzil smoke -v");
    } elsif ($options{mode} eq 'bootstrap') {
        system("dzil authordeps | cpanm");
        system("dzil listdeps | cpanm");
    } elsif ($options{mode} eq 'new_handler') {
        $metal->new_handler({
            name => $options{name},
            type => $options{type},
        });
    } else {
        print STDOUT "Metal was run but no options were called\n";
        _usage();
    }

    return 0;
}

################################################################################

sub _usage {
    pod2usage({
        -exitval => 2,
        -output  => \*STDERR,
        -verbose => 0,
    });
}

################################################################################

exit main();
__END__

=head1 NAME

Metal - IRC framework

=head1 DESCRIPTION

Run various tasks relating to the Metal IRC framework.

=head1 SYNOPSIS

    ./bin/metal -mode bootstrap                                 # install dependencies
    ./bin/metal -mode test                                      # run unit tests
    ./bin/metal -r                                              # run the bot
    ./bin/metal -mode new_handler -name Internal::Some::Handler # create a new handler

=head1 AUTHOR

Mike Jones L<email:mike@netsplit.org.uk>

